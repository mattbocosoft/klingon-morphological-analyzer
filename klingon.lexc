! REFERENCES
! https://en.wikipedia.org/wiki/Klingon_grammar
! https://www.youtube.com/watch?v=e5Did-eVQDc

Multichar_Symbols [AUGMENTATIVE] [DIMINUTIVE] [ENDEARMENT] [SOCALLED] [DUBITATIVE] [ACCURATE] [THIS] [THAT] [LOCATIVE] [ABLATIVE] [CAUSAL] [DATIVE] [TOPIC] [ALL-OVER-THE-PLACE] @U.NOUNTYPE.BODYPART@ @U.NOUNTYPE.CAPABLELANGUAGE@ @U.NOUNTYPE.OTHER@ [NEG] [1P] [2P] [3P] [SG] [PL] [PL-IRREGULAR] [OBJ-1P] [OBJ-2P] [OBJ-3P] [OBJ-SG] [OBJ-PL] [INDICATIVE] [IMPERATIVE] [REFLEXIVE] [RECIPROCAL] [REQUIRED] [WILLING] [PREPARED] [SET-UP] [AFRAID] [STATECHANGE] [RESUME] [CAUSATIVE] [UNDEFINED-SUBJ] [CAPABLE] [BEST] [CERTAIN] [UNCERTAIN] [OBVIOUS] [PERFECT] [PERFECTPURPOSE] [CONTINUOUS] [CONTINUOUSEND] [HONOR] [PERSON] [DVN] [INTEROGATIVE] [BEFORE] [SIMULTANEOUS] [AFTER] [CONDITIONAL] [CAUSE] [RELATIVE] [PURPOSE] [REFUSE] [EMPHASIS] [REVERSE] @U.MOOD.INDICATIVE@ @U.MOOD.IMPERATIVE@ @U.MOOD.INFINITIVE@ @D.MOOD@ @R.MOOD@ @D.VERBTYPE1.ON@ @P.VERBTYPE1.ON@ @D.VERBTYPE2.ON@ @P.VERBTYPE2.ON@ @D.VERBTYPE3.ON@ @P.VERBTYPE3.ON@ @D.VERBTYPE4.ON@ @P.VERBTYPE4.ON@ @D.VERBTYPE5.ON@ @P.VERBTYPE5.ON@ @D.VERBTYPE6.ON@ @P.VERBTYPE6.ON@ @D.VERBTYPE7.ON@ @P.VERBTYPE7.ON@ @D.VERBTYPE8.ON@ @P.VERBTYPE8.ON@ @D.VERBTYPE9.ON@ @P.VERBTYPE9.ON@ @P.INDICATIVE.UNSPECIFIED@ @R.INDICATIVE.UNSPECIFIED@ @C.INDICATIVE@ @D.INDICATIVE.UNSPECIFIED@ @P.VERBSUFFIX.CONTINUOUS@ @R.VERBSUFFIX.CONTINUOUS@ @U.VERBTYPE.STATIVE@ @D.VERBTYPE.STATIVE@ @R.VERBTYPE.STATIVE@ @U.VERBTYPE.ACTION@ [ORDINAL] [REPEATCOUNT] @P.POS.ADVERB@ @D.POS.ADVERB@ @R.POS.ADVERB@ [ADV] [NUMBER] @P.MULTIPLE1234567.ON@ @D.MULTIPLE1234567.ON@ @P.MULTIPLE123456.ON@ @D.MULTIPLE123456.ON@ @P.MULTIPLE12345.ON@ @D.MULTIPLE12345.ON@ @P.MULTIPLE1234.ON@ @D.MULTIPLE1234.ON@ @P.MULTIPLE123.ON@ @D.MULTIPLE123.ON@ @P.MULTIPLE12.ON@ @D.MULTIPLE12.ON@ @P.PLURAL.IRREGULAR@ @D.PLURAL.IRREGULAR@ @P.SINGULAR.IRREGULAR@ @D.SINGULAR.IRREGULAR@ @R.SINGULAR.IRREGULAR@ [POSSESSIVE-1P-SG] [POSSESSIVE-2P-SG] [POSSESSIVE-3P-SG] [POSSESSIVE-1P-PL] [POSSESSIVE-2P-PL] [POSSESSIVE-3P-PL] [PRONOUN] [CONJ] [N] [V] [ADV] [ADJ] [EXCLAMATION]

LEXICON Root
						Verb; ! Includes Adjective part-of-speech via adjectival verbs
						Noun;
						LeftOverWordForms;

! ##########################
! ########## VERB ##########
! ##########################

LEXICON Verb
						VerbPrefix;

LEXICON VerbPrefix
@U.MOOD.INDICATIVE@				VerbIndicativePrefix;
@U.MOOD.IMPERATIVE@				VerbImperativePrefix;
@U.MOOD.INFINITIVE@				VerbStem;


LEXICON VerbIndicativePrefix
[1P][SG]:0					VerbIndicativePrefix1PSG;
[2P][SG]:0					VerbIndicativePrefix2PSG;
[3P][SG]:0					VerbIndicativePrefix3PSG;
[1P][PL]:0					VerbIndicativePrefix1PPL;
[2P][PL]:0					VerbIndicativePrefix2PPL;
[3P][PL]:0					VerbIndicativePrefix3PPL;
@P.INDICATIVE.UNSPECIFIED@			VerbIndicativePrefixUnspecified;

LEXICON VerbIndicativePrefix1PSG
0:jI						VerbStem; ! No Object
![OBJ-1P][OBJ-SG]:0				VerbStem;
[OBJ-2P][OBJ-SG]:qa				VerbStem;
[OBJ-3P][OBJ-SG]:vI				VerbStem;
![OBJ-1P][OBJ-PL]:0				VerbStem;
[OBJ-2P][OBJ-PL]:Sa				VerbStem;
[OBJ-3P][OBJ-PL]:vI				VerbStem;

LEXICON VerbIndicativePrefix2PSG
0:bI						VerbStem; ! No Object
[OBJ-1P][OBJ-SG]:cho				VerbStem;
![OBJ-2P][OBJ-SG]:0				VerbStem;
[OBJ-3P][OBJ-SG]:Da				VerbStem;
[OBJ-1P][OBJ-PL]:ju				VerbStem;
![OBJ-2P][OBJ-PL]:0				VerbStem;
[OBJ-3P][OBJ-PL]:Da				VerbStem;

LEXICON VerbIndicativePrefix3PSG
						VerbStem; ! No Object
[OBJ-1P][OBJ-SG]:mu				VerbStem;
[OBJ-2P][OBJ-SG]:Du				VerbStem;
[OBJ-3P][OBJ-SG]:0				VerbStem;
[OBJ-1P][OBJ-PL]:nu				VerbStem;
[OBJ-2P][OBJ-PL]:lI				VerbStem;
[OBJ-3P][OBJ-PL]:0				VerbStem;

LEXICON VerbIndicativePrefix1PPL
0:ma						VerbStem; ! No Object
![OBJ-1P][OBJ-SG]:0				VerbStem;
[OBJ-2P][OBJ-SG]:pI				VerbStem;
[OBJ-3P][OBJ-SG]:wI				VerbStem;
![OBJ-1P][OBJ-PL]:0				VerbStem;
[OBJ-2P][OBJ-PL]:re				VerbStem;
[OBJ-3P][OBJ-PL]:DI				VerbStem;

LEXICON VerbIndicativePrefix2PPL
0:Su						VerbStem; ! No Object
[OBJ-1P][OBJ-SG]:tu				VerbStem;
![OBJ-2P][OBJ-SG]:0				VerbStem;
[OBJ-3P][OBJ-SG]:bo				VerbStem;
[OBJ-1P][OBJ-PL]:che				VerbStem;
![OBJ-2P][OBJ-PL]:0				VerbStem;
[OBJ-3P][OBJ-PL]:bo				VerbStem;

LEXICON VerbIndicativePrefix3PPL
						VerbStem; ! No Object
[OBJ-1P][OBJ-SG]:mu				VerbStem;
[OBJ-2P][OBJ-SG]:nI				VerbStem;
[OBJ-3P][OBJ-SG]:lu				VerbStem;
[OBJ-1P][OBJ-PL]:nu				VerbStem;
[OBJ-2P][OBJ-PL]:lI				VerbStem;
[OBJ-3P][OBJ-PL]:0				VerbStem;

LEXICON VerbIndicativePrefixUnspecified
						VerbStem; ! No Object
[OBJ-1P][OBJ-SG]:vI				VerbStem;
[OBJ-2P][OBJ-SG]:Da				VerbStem;
[OBJ-3P][OBJ-SG]:0				VerbStem;
[OBJ-1P][OBJ-PL]:wI				VerbStem;
[OBJ-2P][OBJ-PL]:bo				VerbStem;
[OBJ-3P][OBJ-PL]:lu				VerbStem;


LEXICON VerbImperativePrefix
[2P][SG]:yI					VerbStem;
[2P][SG][OBJ-1P][OBJ-SG]:HI			VerbStem;
[2P][SG][OBJ-3P][OBJ-SG]:yI			VerbStem;
[2P][SG][OBJ-1P][OBJ-PL]:gho			VerbStem;
[2P][SG][OBJ-3P][OBJ-PL]:tI			VerbStem;
[2P][PL]:pe					VerbStem;
[2P][PL][OBJ-1P][OBJ-SG]:HI			VerbStem;
[2P][PL][OBJ-3P][OBJ-SG]:yI			VerbStem;
[2P][PL][OBJ-1P][OBJ-PL]:gho			VerbStem;
[2P][PL][OBJ-3P][OBJ-PL]:tI			VerbStem;


LEXICON VerbStem
@U.VERBTYPE.STATIVE@				StativeVerb;
@U.VERBTYPE.ACTION@				ActionVerb;

LEXICON StativeVerb
Dach[to-be-absent]:Dach			VerbSuffix;
loj[to-be-all-gone]:loj			VerbSuffix;
tIn[to-be-big]:tIn				VerbSuffix;
Dal[to-be-boring]:Dal				VerbSuffix;
jot[to-be-calm]:jot				VerbSuffix;
Say'[to-be-clean]:Say'				VerbSuffix;
bIr[to-be-cold]:bIr				VerbSuffix;
mIS[to-be-confused]:mIS			VerbSuffix;
lam[to-be-dirty]:lam				VerbSuffix;
chech[to-be-drunk]:chech			VerbSuffix;
puS[to-be-few]:puS				VerbSuffix;
Do'[to-be-fortunate]:Do'			VerbSuffix;
QaQ[to-be-good]:QaQ				VerbSuffix;
let[to-be-hard-like-a-rock]:let		VerbSuffix;
Quch[to-be-happy]:Quch				VerbSuffix;
ghung[to-be-hungry]:ghung			VerbSuffix;
Doj[to-be-impressive]:Doj			VerbSuffix;
Daj[to-be-interesting]:Daj			VerbSuffix;
buD[to-be-lazy]:buD				VerbSuffix;
rur[to-be-like]:rur				VerbSuffix;
tIq[to-be-long]:tIq				VerbSuffix;
law'[to-be-many]:law'				VerbSuffix;
ghIH[to-be-messy,to-be-sloppy]:ghIH		VerbSuffix;
Sum[to-be-near]:Sum				VerbSuffix;
bIt[to-be-nervous]:bIt				VerbSuffix;
chu'[to-be-new]:chu'				VerbSuffix;
chuS[to-be-noisy]:chuS				VerbSuffix;
Doq[to-be-orange,to-be-red]:Doq		VerbSuffix;
bel[to-be-pleased]:bel				VerbSuffix;
Hem[to-be-proud]:Hem				VerbSuffix;
Hev[to-receive]:Hev				VerbSuffix;
Hop[to-be-remote,to-be-far]:Hop		VerbSuffix;
lugh[to-be-right,to-be-correct]:lugh		VerbSuffix;
ghegh[to-be-rough]:ghegh			VerbSuffix;
mach[to-be-small]:mach				VerbSuffix;
jej[to-be-sharp]:jej				VerbSuffix;
run[to-be-short]:run				VerbSuffix;
Hab[to-be-smooth]:Hab				VerbSuffix;
le'[to-be-special,to-be-exceptional]:le'	VerbSuffix;
HoS[to-be-strong]:HoS				VerbSuffix;
woch[to-be-tall]:woch				VerbSuffix;
rap[to-be-the-same]:rap			VerbSuffix;
Doy'[to-be-tired]:Doy'				VerbSuffix;
yoy[to-be-upside-down]:yoy			VerbSuffix;
lI'[to-be-useful]:lI'				VerbSuffix;
yIQ[to-be-wet]:yIQ				VerbSuffix;
chIS[to-be-white]:chIS				VerbSuffix;



LEXICON ActionVerb
laj[to-accept]:laj				VerbSuffix;
chav[to-achieve]:chav				VerbSuffix;
chel[to-add]:chel				VerbSuffix;
chID[to-admit]:chID				VerbSuffix;
qeS[to-advise]:qeS				VerbSuffix;
chaw'[to-allow]:chaw'				VerbSuffix;
choH[to-alter]:choH				VerbSuffix;
poj[to-analyze]:poj				VerbSuffix;
jang[to-answer,to-reply]:jang			VerbSuffix;
HIv[to-attack]:HIv				VerbSuffix;
jol[to-beam-aboard]:jol			VerbSuffix;
Har[to-believe]:Har				VerbSuffix;
chop[to-bite]:chop				VerbSuffix;
ghor[to-break]:ghor				VerbSuffix;
je'[to-buy,to-purchase]:je'			VerbSuffix;
SaH[to-care]:SaH				VerbSuffix;
tlha'[to-chase]:tlha'				VerbSuffix;
So'[to-cloak,to-hide]:So'			VerbSuffix;
chol[to-close-in,to-get-closer]:chol		VerbSuffix;
ra'[to-command]:ra'				VerbSuffix;
buS[to-concentrate-on]:buS			VerbSuffix;
chargh[to-conquer]:chargh			VerbSuffix;
ngaS[to-contain]:ngaS				VerbSuffix;
SeH[to-control]:SeH				VerbSuffix;
baghHa'[to-decrypt]:baghHa'			VerbSuffix;
Del[to-describe]:Del				VerbSuffix;
Qaw'[to-destroy]:Qaw'				VerbSuffix;
Hegh[to-die]:Hegh				VerbSuffix;
Hon[to-doubt]:Hon				VerbSuffix;
chagh[to-drop]:chagh				VerbSuffix;
Sop[to-eat]:Sop					VerbSuffix;
cher[to-establish]:cher			VerbSuffix;
je'[to-feed]:je'				VerbSuffix;
tu'[to-find]:tu'				VerbSuffix;
baH[to-fire]:baH				VerbSuffix;
joq[to-flutter]:joq				VerbSuffix;
lIj[to-forget]:lIj				VerbSuffix;
nob[to-give]:nob				VerbSuffix;
jaH[to-go,to-travel]:jaH			VerbSuffix;
ghoS[to-go-onward,to-approach]:ghoS		VerbSuffix;
ghaj[to-have,to-possess]:ghaj			VerbSuffix;
qIp[to-hit]:qIp					VerbSuffix;
tul[to-hope]:tul				VerbSuffix;
Dub[to-improve]:Dub				VerbSuffix;
Hech[to-intend]:Hech				VerbSuffix;
HoH[to-kill]:HoH				VerbSuffix;
Hagh[to-laugh]:Hagh				VerbSuffix;
Dev[to-lead,to-guide]:Dev			VerbSuffix;
ghoj[to-learn]:ghoj				VerbSuffix;
ghom[to-meet]:ghom				VerbSuffix;
chIj[to-navigate]:chIj				VerbSuffix;
Sutlh[to-negotiate]:Sutlh			VerbSuffix;
DIl[to-pay]:DIl					VerbSuffix;
bot[to-prevent]:bot				VerbSuffix;
chep[to-prosper]:chep				VerbSuffix;
laD[to-read]:laD				VerbSuffix;
ghov[to-recognize]:ghov			VerbSuffix;
chup[to-recommend,to-suggest]:chup		VerbSuffix;
qaw[to-rememeber]:qaw				VerbSuffix;
leS[to-rest,to-relax]:leS			VerbSuffix;
chegh[to-return-to]:chegh			VerbSuffix;
'ang[to-reveal]:'ang				VerbSuffix;
leng[to-roam,to-travel]:leng			VerbSuffix;
jatlh[to-say,to-tell]:jatlh			VerbSuffix;
Hotlh[to-scan]:Hotlh				VerbSuffix;
ghIj[to-scare]:ghIj				VerbSuffix;
jach[to-scream]:jach				VerbSuffix;
legh[to-see]:legh				VerbSuffix;
ngev[to-sell]:ngev				VerbSuffix;
bach[to-shoot]:bach				VerbSuffix;
bom[to-sing]:bom				VerbSuffix;
ba'[to-sit]:ba'					VerbSuffix;
Qong[to-sleep]:Qong				VerbSuffix;
HaD[to-study]:HaD				VerbSuffix;
ghoD[to-stuff]:ghoD				VerbSuffix;
Qap[to-succeed]:Qap				VerbSuffix;
jegh[to-surrender,to-give-up]:jegh		VerbSuffix;
tlhap[to-take]:tlhap				VerbSuffix;
chen[to-take-form]:chen			VerbSuffix;
ja'[to-tell,to-report]:ja'			VerbSuffix;
toj[to-trick]:toj				VerbSuffix;
yaj[to-understand]:yaj				VerbSuffix;
lo'[to-use]:lo'					VerbSuffix;
loS[to-wait]:loS				VerbSuffix;
bej[to-watch]:bej				VerbSuffix;
ghItlh[to-write]:ghItlh			VerbSuffix;


LEXICON VerbSuffix
@D.VERBTYPE9.ON@				VerbRoversSuffix;
						VerbType1Gate;
						VerbType2Gate;
						VerbType3Gate;
						VerbType4Gate;
						VerbType5Gate;
						VerbType6Gate;
						VerbType7Gate;
						VerbType8Gate;
						VerbType9Gate;
[V]:0						VerbMoodTag;
[V]@U.MOOD.INFINITIVE@@U.VERBTYPE.STATIVE@:@U.MOOD.INFINITIVE@@U.VERBTYPE.STATIVE@			NounType5Suffix; ! Type-5 noun suffixes that would normally be attached to the noun are instead attached to the ADJECTIVAL (stative) verb;


LEXICON VerbRoversSuffix
! "These suffixes have no defined position, and can go after the verb stem or after any suffix – even another rover – except after a type-9 suffix or where the result would be meaningless. They modify whatever directly precedes them."
[NEG]@U.MOOD.INDICATIVE@:@U.MOOD.INDICATIVE@be'			VerbSuffix;
[NEG]@U.MOOD.IMPERATIVE@:@U.MOOD.IMPERATIVE@Qo'			VerbSuffix;
[REFUSE]@U.MOOD.INDICATIVE@:@U.MOOD.INDICATIVE@Qo'			VerbSuffix;
[EMPHASIS]:qu'								VerbSuffix;
[REVERSE]:Ha'								VerbSuffix;

LEXICON VerbType1Gate
@D.VERBTYPE1.ON@@P.VERBTYPE1.ON@		VerbType1Suffix;

LEXICON VerbType1Suffix ! 2 Count = reflexive/reciprocal
[REFLEXIVE]:'egh				VerbSuffix;
[RECIPROCAL]:chuq				VerbSuffix;


LEXICON VerbType2Gate
@P.VERBTYPE1.ON@@D.VERBTYPE2.ON@@P.VERBTYPE2.ON@		VerbType2Suffix;

LEXICON VerbType2Suffix ! 5 Count = volition/necessity
[REQUIRED]:nIS					VerbSuffix;
[WILLING]:qang					VerbSuffix;
[PREPARED]:rup					VerbSuffix;
[SET-UP]:beH					VerbSuffix;
[AFRAID]:vIp					VerbSuffix;


LEXICON VerbType3Gate
@P.VERBTYPE1.ON@@P.VERBTYPE2.ON@@D.VERBTYPE3.ON@@P.VERBTYPE3.ON@		VerbType3Suffix;

LEXICON VerbType3Suffix ! 2 Count = inceptive/inchoative
[STATECHANGE]:choH				VerbSuffix;
[RESUME]:qa'					VerbSuffix;


LEXICON VerbType4Gate
@P.VERBTYPE1.ON@@P.VERBTYPE2.ON@@P.VERBTYPE3.ON@@D.VERBTYPE4.ON@@P.VERBTYPE4.ON@		VerbType4Suffix;

LEXICON VerbType4Suffix ! 1 Count = causative
[CAUSATIVE]:moH					VerbSuffix;


LEXICON VerbType5Gate
@P.VERBTYPE1.ON@@P.VERBTYPE2.ON@@P.VERBTYPE3.ON@@P.VERBTYPE4.ON@@D.VERBTYPE5.ON@@P.VERBTYPE5.ON@		VerbType5Suffix;

LEXICON VerbType5Suffix ! 2 Count = undefined subject; capability
[UNDEFINED-SUBJ]@R.INDICATIVE.UNSPECIFIED@@C.INDICATIVE@:@R.INDICATIVE.UNSPECIFIED@@C.INDICATIVE@lu'				VerbSuffix;
[CAPABLE]:laH					VerbSuffix;


LEXICON VerbType6Gate
@P.VERBTYPE1.ON@@P.VERBTYPE2.ON@@P.VERBTYPE3.ON@@P.VERBTYPE4.ON@@P.VERBTYPE5.ON@@D.VERBTYPE6.ON@@P.VERBTYPE6.ON@		VerbType6Suffix;

LEXICON VerbType6Suffix ! 4 Count = perfection; uncertainty
[BEST]:chu'					VerbSuffix;
[CERTAIN]:bej					VerbSuffix;
[UNCERTAIN]:law'				VerbSuffix;
[OBVIOUS]:ba'					VerbSuffix;


LEXICON VerbType7Gate
@P.VERBTYPE1.ON@@P.VERBTYPE2.ON@@P.VERBTYPE3.ON@@P.VERBTYPE4.ON@@P.VERBTYPE5.ON@@P.VERBTYPE6.ON@@D.VERBTYPE7.ON@@P.VERBTYPE7.ON@		VerbType7Suffix;

LEXICON VerbType7Suffix ! 4 Count = aspect
[PERFECT]:pu'					VerbSuffix;
[PERFECTPURPOSE]:ta'				VerbSuffix;
[CONTINUOUS]@P.VERBSUFFIX.CONTINUOUS@:@P.VERBSUFFIX.CONTINUOUS@taH		VerbSuffix;
[CONTINUOUSEND]:lI'				VerbSuffix;


LEXICON VerbType8Gate
@P.VERBTYPE1.ON@@P.VERBTYPE2.ON@@P.VERBTYPE3.ON@@P.VERBTYPE4.ON@@P.VERBTYPE5.ON@@P.VERBTYPE6.ON@@P.VERBTYPE7.ON@@D.VERBTYPE8.ON@@P.VERBTYPE8.ON@		VerbType8Suffix;

LEXICON VerbType8Suffix ! 1 Count = honorific
[HONOR]:neS					VerbSuffix;


LEXICON VerbType9Gate
@P.VERBTYPE1.ON@@P.VERBTYPE2.ON@@P.VERBTYPE3.ON@@P.VERBTYPE4.ON@@P.VERBTYPE5.ON@@P.VERBTYPE6.ON@@P.VERBTYPE7.ON@@P.VERBTYPE8.ON@@D.VERBTYPE9.ON@@P.VERBTYPE9.ON@		VerbType9Suffix;

LEXICON VerbType9Suffix ! 11 Count = syntactic
@U.MOOD.INFINITIVE@[PERSON]:@U.MOOD.INFINITIVE@wI'					NounSuffix; ! Nominalizer. Verbs can take the Type 9 suffix -wI' one/thing which does, and the resulting word is a noun which can take all noun suffixes. Other verb suffixes can be added before -wI', provided no other Type 9 suffix is used.
[DVN]:ghach					VerbSuffix; ! Nominalizer, '-ness' "Turns a verb (which must have at least one other suffix attached) into a noun. The use of this suffix often makes for bad Klingon, and it is strongly suggested that you refrain from using any word with -ghach, unless it is found in the dictionary." -http://klingonska.org/dict/suffix.html
[INTEROGATIVE]:'a'				VerbSuffix; ! Modal
[OPTATIVE]:jaj					VerbSuffix; ! Modal
[BEFORE]:pa'					VerbSuffix; ! Subordinator
[SIMULTANEOUS]@R.VERBSUFFIX.CONTINUOUS@:vIS@R.VERBSUFFIX.CONTINUOUS@				VerbSuffix; ! Subordinator - It is always used in conjunction with the type-7 suffix continuous suffix -taH.
[AFTER]:DI'					VerbSuffix; ! Subordinator
[CONDITIONAL]:chugh				VerbSuffix; ! Cause and effect
[CAUSE]:mo'					VerbSuffix; ! Cause and effect
[RELATIVE]:bogh					VerbSuffix; ! Relative and purpose clause
[PURPOSE]:meH					VerbSuffix; ! Relative and purpose clause


LEXICON VerbMoodTag
[INDICATIVE]@U.MOOD.INDICATIVE@@D.INDICATIVE.UNSPECIFIED@:@U.MOOD.INDICATIVE@@D.INDICATIVE.UNSPECIFIED@			#;
[IMPERATIVE]@U.MOOD.IMPERATIVE@:@U.MOOD.IMPERATIVE@										#;


 
! ##########################
! ########## NOUN ##########
! ##########################


LEXICON Noun
@U.NOUNTYPE.BODYPART@				NounBodyPart;
@U.NOUNTYPE.CAPABLELANGUAGE@			NounCapableOfLanguage;
@U.NOUNTYPE.OTHER@				NounOther;

! LEXICON NounName
! be'etor[B'Etor]:be'etor			NounSuffix;

LEXICON NounBodyPart
DeS[arm]:DeS					NounSuffix;
mIn[eye]:mIn					NounSuffix;
qam[foot]:qam					NounSuffix;
ghop[hand]:ghop					NounSuffix;
'uS[leg]:'uS					NounSuffix;
ghIch[nose]:ghIch				NounSuffix;
DIr[skin]:DIr					NounSuffix;


LEXICON NounCapableOfLanguage
loDHom[boy]:loDHom				NounSuffix;
loDnI'[brother]:loDnI'				NounSuffix;
je'wI'[buyer]:je'wI'				NounSuffix;
HoD[captain]:HoD				NounSuffix;
puq[child]:puq					NounSuffix;
vav[father]:vav					NounSuffix;
jup[friend]:jup					NounSuffix;
be'Hom[girl]:be'Hom				NounSuffix;
meb[guest]:meb					NounSuffix;
baHwI'[gunner]:baHwI'				NounSuffix;
DeghwI'[helmsman]:DeghwI'			NounSuffix;
loDnal[husband]:loDnal				NounSuffix;
loD[male,man]:loD				NounSuffix;
ghot[person]:ghot				NounSuffix;
Duy[representative]:Duy			NounSuffix;
qetwI'[runner]:qetwI'				NounSuffix;
bu'[sergeant]:bu'				NounSuffix;
be'nI'[sister]:be'nI'				NounSuffix;
be'nal[wife]:be'nal				NounSuffix;
be'[woman]:be'					NounSuffix;


LEXICON NounOther
laH[ability]:laH				NounSuffix;
latlh[additional-one]:latlh			NounSuffix;
Hoch[all,everything]:Hoch			NounSuffix;
Ha'DIbaH[animal,meat]:Ha'DIbaH			NounSuffix;
chav[achievement]:chav				NounSuffix;
ngup[authority]:ngup				NounSuffix;
'alnIl[belt,buckle]:'alnIl			NounSuffix;
DaS[boot]:DaS					NounSuffix;
jaj[day]:jaj					NounSuffix;
quS[chair]:quS					NounSuffix;
choH[change]:choH				NounSuffix;
veng[city]:veng					NounSuffix;
qoj[cliff]:qoj					NounSuffix;
wep[coat]:wep					NounSuffix;
De'wI'[computer]:De'wI'			NounSuffix;
Hatlh[country]:Hatlh				NounSuffix;
De'[data,information]:De'			NounSuffix;
lojmIt[door,gate]:lojmIt			NounSuffix;
jagh[enemy]:jagh				NounSuffix;
Qagh[error]:error				NounSuffix;
Soj[food]:Soj					NounSuffix;
jup[friend]:jup					NounSuffix;
HIvje'[glass,tumble]:HIvje'			NounSuffix;
ghom[group]:ghom				NounSuffix;
QaH[help]:QaH					NounSuffix;
Degh[helm]:Degh					NounSuffix;
juH[home,house]:juH				NounSuffix;
qach[idea]:qach					NounSuffix;
Hol[language]:Hol				NounSuffix;
bang[love]:bang					NounSuffix;
ghItlh[manuscript]:ghItlh			NounSuffix;
Qagh[mistake]:Qagh				NounSuffix;
jar[month]:jar					NounSuffix;
Dap[nonsense]:Dap				NounSuffix;
nav[paper]:nav					NounSuffix;
roj[peace]:roj					NounSuffix;
Saj[pet]:Saj					NounSuffix;
yuQ[planet]:yuQ					NounSuffix;
bel[pleasure]:bel				NounSuffix;
woQ[power]:woQ					NounSuffix;
bortaS[revenge]:bortaS				NounSuffix;
pa'[room]:pa'					NounSuffix;
bach[shot]:bach					NounSuffix;
Duj[ship,vessel]:Duj				NounSuffix;
ghu'[situation]:ghu'				NounSuffix;
chal[sky]:chal					NounSuffix;
Hal[source]:Hal					NounSuffix;
Hov[star]:Hov					NounSuffix;
'ejyo'[starfleet]:'ejyo'			NounSuffix;
'ejDo'[starship]:'ejDo'			NounSuffix;
lut[story]:lut					NounSuffix;
Duj[ship]:Duj					NounSuffix;
baghneQ[spoon]:baghneQ				NounSuffix;
targh[targ]:targh				NounSuffix; ! A "targ" is a Klingon animal somewhat resembling a boar
DoS[target]@P.SINGULAR.IRREGULAR@:DoS@P.SINGULAR.IRREGULAR@		NounSuffix;
ray'[targets]@P.PLURAL.IRREGULAR@:ray'@P.PLURAL.IRREGULAR@		NounSuffix;
Doch[thing]:Doch				NounSuffix;
vIj[thruster]@P.SINGULAR.IRREGULAR@:vIj@P.SINGULAR.IRREGULAR@	NounSuffix;
chuyDaH[thrusters]@P.PLURAL.IRREGULAR@:chuyDaH@P.PLURAL.IRREGULAR@	NounSuffix;
DaHjaj[today]:DaHjaj				NounSuffix;
peng[torpedo]@P.SINGULAR.IRREGULAR@:peng@P.SINGULAR.IRREGULAR@	NounSuffix;
cha[torpedoes]@P.PLURAL.IRREGULAR@:cha@P.PLURAL.IRREGULAR@		NounSuffix;
ghogh[voice]:ghogh				NounSuffix;
bIQ[water]:bIQ					NounSuffix;
Hogh[week]:Hogh					NounSuffix;
SuS[wind,breeze]:SuS				NounSuffix;
DIS[year]:DIS					NounSuffix;
						NounOtherLocation;

LEXICON NounOtherLocation
vogh[somewhere]:vogh				NounSuffix;
nIH[right-side]:nIH				NounSuffix;
poS[left-side]:poS				NounSuffix;
tlhop[area-in-front]:tlhop			NounSuffix;
'em[area-in-back]:'em				NounSuffix;
Dung[area-above]:Dung				NounSuffix;
bIng[area-below]:bIng				NounSuffix;
retlh[area-beside]:retlh			NounSuffix;
joj[area-between]:joj				NounSuffix;
qoD[inside]:qoD					NounSuffix;
Hur[outside]:Hur				NounSuffix;


LEXICON NounSuffix
						NounType1Suffix;

LEXICON NounType1Suffix ! size, affection
[AUGMENTATIVE]:'a'				NounType2Suffix;
[DIMINUTIVE]:Hom				NounType2Suffix;
[ENDEARMENT]:-oy				NounType2Suffix; ! The dash character will either be discarded or turned into a glottal stop in the foma file
						NounType2Suffix; ! Type 1 suffix not required, jump to next type


! A noun does not require a plural suffix if a pronoun or pronominal prefix serves to indicate that it is plural,[10] or if it is being used in conjunction with a number.
LEXICON NounType2Suffix ! number
[PL]@D.SINGULAR.IRREGULAR@@D.PLURAL.IRREGULAR@@U.NOUNTYPE.CAPABLELANGUAGE@:@D.SINGULAR.IRREGULAR@@D.PLURAL.IRREGULAR@pu'@U.NOUNTYPE.CAPABLELANGUAGE@				NounType3Suffix;
[PL]@D.SINGULAR.IRREGULAR@@D.PLURAL.IRREGULAR@@U.NOUNTYPE.BODYPART@:@D.SINGULAR.IRREGULAR@@D.PLURAL.IRREGULAR@Du'@U.NOUNTYPE.BODYPART@						NounType3Suffix;
[PL]@D.SINGULAR.IRREGULAR@@D.PLURAL.IRREGULAR@@U.NOUNTYPE.OTHER@:@D.SINGULAR.IRREGULAR@@D.PLURAL.IRREGULAR@@U.NOUNTYPE.OTHER@mey							NounType3Suffix;
[PL][ALL-OVER-THE-PLACE]@R.SINGULAR.IRREGULAR@:@R.SINGULAR.IRREGULAR@mey	NounType3Suffix; ! The singular [irregular] forms may take the {-mey} suffix, but the meaning always carries the ``scattered all about'' connotation
[PL][ALL-OVER-THE-PLACE]@D.PLURAL.IRREGULAR@@U.NOUNTYPE.CAPABLELANGUAGE@:@D.PLURAL.IRREGULAR@@U.NOUNTYPE.CAPABLELANGUAGE@mey	NounType3Suffix; ! When -mey is used for nouns that would normally take -pu’ or -Du’, it carries the connotation of being all over the place.
[PL]@D.SINGULAR.IRREGULAR@:@D.SINGULAR.IRREGULAR@0						NounType3Suffix; ! A plural suffix is never obligatory
[SG]@D.PLURAL.IRREGULAR@:@D.PLURAL.IRREGULAR@	NounType3Suffix;


LEXICON NounType3Suffix ! accuracy
[SOCALLED]:qoq					NounType4Suffix;
[DUBITATIVE]:Hey				NounType4Suffix;
[ACCURATE]:na'					NounType4Suffix;
						NounType4Suffix;  ! Type 3 suffix not required, jump to next type


LEXICON NounType4Suffix ! possession, determiners
[POSSESSIVE-1P-SG]:wIj					NounType5Suffix;
[POSSESSIVE-1P-SG]@U.NOUNTYPE.CAPABLELANGUAGE@:wI'@U.NOUNTYPE.CAPABLELANGUAGE@				NounType5Suffix;
[POSSESSIVE-2P-SG]:lIj					NounType5Suffix;
[POSSESSIVE-2P-SG]@U.NOUNTYPE.CAPABLELANGUAGE@:lI'@U.NOUNTYPE.CAPABLELANGUAGE@				NounType5Suffix;
[POSSESSIVE-3P-SG]:Daj					NounType5Suffix;
[POSSESSIVE-1P-PL]:maj					NounType5Suffix;
[POSSESSIVE-1P-PL]@U.NOUNTYPE.CAPABLELANGUAGE@:ma'@U.NOUNTYPE.CAPABLELANGUAGE@				NounType5Suffix;
[POSSESSIVE-2P-PL]:raj					NounType5Suffix;
[POSSESSIVE-2P-PL]@U.NOUNTYPE.CAPABLELANGUAGE@:ra'@U.NOUNTYPE.CAPABLELANGUAGE@				NounType5Suffix;
[POSSESSIVE-3P-PL]:chaj					NounType5Suffix;
[THIS]:vam					NounType5Suffix;
[THAT]:vetlh					NounType5Suffix;
						NounType5Suffix;  ! Type 4 suffix not required, jump to next type

LEXICON NounType5Suffix ! syntactic role
[LOCATIVE]:Daq					NounTag;
[ABLATIVE]:vo'					NounTag;
[CAUSAL]:mo'					NounTag;
[DATIVE]:vaD					NounTag;
[TOPIC]:'e'					NounTag;
						NounTag; ! Type 5 suffix not required, jump to next type

LEXICON NounTag
@R.VERBTYPE.STATIVE@				#;
@D.MOOD@[N]:@D.MOOD@				#;
@D.VERBTYPE.STATIVE@@R.MOOD@[DVN]:@D.VERBTYPE.STATIVE@@R.MOOD@						#;



! ####################################################
! ########## LEFT-OVER WORD FORMS (chuvmey) ##########
! ####################################################


LEXICON LeftOverWordForms
						Pronoun;
						Number;
						Conjunction;
						Adverbial;
						Exclamation;
						NameAddress;


! ########## PRONOUN ##########

LEXICON Pronoun
bIH[they,them]@U.NOUNTYPE.OTHER@:bIH@U.NOUNTYPE.OTHER@				PronounTag;
chaH[they,them]@U.NOUNTYPE.CAPABLELANGUAGE@:chaH@U.NOUNTYPE.CAPABLELANGUAGE@	PronounTag;
'e'[that]:'e'					PronounTag; ! Referring to previous part of sentence
ghaH[he,she,him,her]:ghaH			PronounTag;
jIH[I,me]:jIH					PronounTag;
maH[we,us]:maH					PronounTag;
net[that,also]:net				PronounTag; ! Similar to 'e'
'oH[it]:'oH					PronounTag;
SoH[you-SG]:SoH					PronounTag;
tlhIH[you-PL]:tlhIH				PronounTag;

LEXICON PronounTag
[PRONOUN]:0					#;


! ########## NUMBER ##########

LEXICON Number
						NumberDecimal;
						NumberMultiple;

LEXICON NumberDecimal
[zero]:pagh					NumberIntermediateSuffix;
[+one]:wa'					NumberIntermediateSuffix;
[+two]:cha'					NumberIntermediateSuffix;
[+three]:wej					NumberIntermediateSuffix;
[+four]:loS					NumberIntermediateSuffix;
[+five]:vagh					NumberIntermediateSuffix;
[+six]:jav					NumberIntermediateSuffix;
[+seven]:Soch					NumberIntermediateSuffix;
[+eight]:chorgh					NumberIntermediateSuffix;
[+nine]:Hut					NumberIntermediateSuffix;


LEXICON NumberIntermediateSuffix
 						NumberMultiple;
						NumberSuffix;

LEXICON NumberMultiple ! Use gating so that the multiples are used in the correct order and not repeated
						Multiple1234567Gate;
						Multiple123456Gate;
						Multiple12345Gate;
						Multiple1234Gate;
						Multiple123Gate;
						Multiple12Gate;

LEXICON Multiple1234567Gate
@D.MULTIPLE1234567.ON@@P.MULTIPLE1234567.ON@		Multiple1234567;

LEXICON Multiple1234567
[x1,%0%0%0,%0%0%0]:'uy'			NumberSuffix;


LEXICON Multiple123456Gate
@P.MULTIPLE1234567.ON@@D.MULTIPLE123456.ON@@P.MULTIPLE123456.ON@		Multiple123456;

LEXICON Multiple123456
[x1%0%0,%0%0%0]:bIp				NumberSuffix;


LEXICON Multiple12345Gate
@P.MULTIPLE1234567.ON@@P.MULTIPLE123456.ON@@D.MULTIPLE12345.ON@@P.MULTIPLE12345.ON@		Multiple12345;

LEXICON Multiple12345
[x1%0,%0%0%0]:netlh				NumberSuffix;


LEXICON Multiple1234Gate
@P.MULTIPLE1234567.ON@@P.MULTIPLE123456.ON@@P.MULTIPLE12345.ON@@D.MULTIPLE1234.ON@@P.MULTIPLE1234.ON@		Multiple1234;

LEXICON Multiple1234
[x1,%0%0%0]:SanID				NumberSuffix;
[x1,%0%0%0]:SaD					NumberSuffix;


LEXICON Multiple123Gate
@P.MULTIPLE1234567.ON@@P.MULTIPLE123456.ON@@P.MULTIPLE12345.ON@@P.MULTIPLE1234.ON@@D.MULTIPLE123.ON@@P.MULTIPLE123.ON@			Multiple123;

LEXICON Multiple123
[x1%0%0]:vatlh					NumberSuffix;


LEXICON Multiple12Gate
@P.MULTIPLE1234567.ON@@P.MULTIPLE123456.ON@@P.MULTIPLE12345.ON@@P.MULTIPLE1234.ON@@P.MULTIPLE123.ON@@D.MULTIPLE12.ON@@P.MULTIPLE12.ON@			Multiple12;

LEXICON Multiple12
[x1%0]:maH					NumberSuffix;


LEXICON NumberSuffix
0:% 						NumberDecimal; ! Potentially repeat with spaces
[NUMBER]:0					NumberTag;

LEXICON NumberTag
[ORDINAL]@D.POS.ADVERB@:@D.POS.ADVERB@DIch	#; ! If the current part-of-speech is an adverb, then the '-logh' suffix is required
[REPEATCOUNT]:logh				NumberAdverbTag;
@D.POS.ADVERB@					#; ! If the current part-of-speech is an adverb, then the '-logh' suffix is required


LEXICON NumberAdverbTag
@R.POS.ADVERB@[ADV]:@R.POS.ADVERB@		#;
@D.POS.ADVERB@					#;


! ########## CONJUNCTION ##########

LEXICON Conjunction
je[word-and]:je					ConjunctionTag;
joq[word-or]:joq				ConjunctionTag;
ghap[word-xor]:ghap				ConjunctionTag;
'ej[sentence-and]:'ej				ConjunctionTag;
qoj[sentence-or]:qoj				ConjunctionTag;
pagh[sentence-xor]:pagh			ConjunctionTag;
'ach[sentence-contrast]:'ach			ConjunctionTag;
'a[sentence-contrast]:'a			ConjunctionTag;

LEXICON ConjunctionTag
[CONJ]:0					#;



! ########## ADVERBIAL ##########

LEXICON Adverbial
						BasicAdverbs;
						TimeStampAdverbs;
@P.POS.ADVERB@					Number;

LEXICON BasicAdverbs
batlh[honorably]:batlh				AdverbSuffix;
bong[accidentally]:bong			AdverbSuffix;
chaq[perhaps]:chaq				AdverbSuffix;
chIch[purposely]:chIch				AdverbSuffix;
DaH[now]:DaH					AdverbSuffix;
Do'[luckily]:Do'				AdverbSuffix;
ghaytan[likely]:ghaytan			AdverbSuffix;
ghIq[subsequently]:ghIq			AdverbSuffix;
jaS[differently]:jaS				AdverbSuffix;
jay'[intensely]:jay'				AdverbSuffix;
loQ[slightly]:loQ				AdverbSuffix;
motlh[typically]:motlh				AdverbSuffix;
neH[merely]:neH					AdverbSuffix;
nIteb[alone]:nIteb				AdverbSuffix;
nom[quickly]:nom				AdverbSuffix;
not[never]:not					AdverbSuffix;
ngugh[then]:ngugh				AdverbSuffix;
pay'[suddenly]:pay'				AdverbSuffix;
pe'vIl[forcefully]:pe'vIl			AdverbSuffix;
pIj[frequently]:pIj				AdverbSuffix;
qen[recently]:qen				AdverbSuffix;
QIt[slowly]:QIt					AdverbSuffix;
reH[always]:reH					AdverbSuffix;
roD[habitually]:roD				AdverbSuffix;
rut[sometimes]:rut				AdverbSuffix;
SIbI'[immediately]:SIbI'			AdverbSuffix;
tugh[soon]:tugh					AdverbSuffix;
tlhoS[almost]:tlhoS				AdverbSuffix;
tlhoy[excessively]:tlhoy			AdverbSuffix;
vabDot[including]:vabDot			AdverbSuffix;
vaj[accordingly]:vaj				AdverbSuffix;
wej[not-yet]:wej				AdverbSuffix;

LEXICON TimeStampAdverbs
DaHjaj[today]:DaHjaj				AdverbSuffix;
wa'Hu'[yesterday]:wa'Hu'			AdverbSuffix;
wa'leS[tomorrow]:wa'leS			AdverbSuffix;
DaHjaj% po[this-morning]:DaHjaj% po		AdverbSuffix;
DaHjaj% po[this-morning]:DaHjaj% po		AdverbSuffix;
wa'Hu'% ram[last-night]:wa'Hu'% ram		AdverbSuffix;
wa'leS% po[tomorrow-morning]:wa'leS% po	AdverbSuffix;


LEXICON AdverbSuffix
[REVERSE][ADV]:Ha'				#;
[ADV]:0						#; ! No suffix



! ########## EXCLAMATION ##########

LEXICON Exclamation
HISlaH[yes]:HISlaH				ExclamationSuffix;
HIja'[yes]:HIja'				ExclamationSuffix;
ghobe'[no]:ghobe'				ExclamationSuffix;
Ha'[let's-go]:Ha'				ExclamationSuffix;
Qapla'[success]:Qapla'				ExclamationSuffix;
lu'[yes,okay-I-will]:lu'			ExclamationSuffix;
maj[Good,express-satisfaction]:maj		ExclamationSuffix;
majQa'[Very-good,Well-done]:majQa'		ExclamationSuffix;
nuqneH[What-do-you-want]:nuqneH		ExclamationSuffix;
pItlh[Done!]:pItlh				ExclamationSuffix;
Qo'[No,I-won't,I-refuse]:Qo'			ExclamationSuffix;
SuH[Ready%!]:SuH				ExclamationSuffix;
toH[Well%!]:toH					ExclamationSuffix;
wejpuH[Charming(ironic)]:wejpuH		ExclamationSuffix;
'eH[Ready%!]:'eH				ExclamationSuffix;

LEXICON ExclamationSuffix
[EXCLAMATION]:0					#;



! ########## NAME & ADDRESS ##########

LEXICON NameAddress
mara[Mara]:mara					#;
matlh[Maltz]:matlh				#;
qeng[Kang]:qeng					#;
qeylIS[Kahless]:qeylIS				#;qolotlh[Koloth]:qolotlh			#;qor[Kor]:qor					#;qoreQ[Korax]:qoreQ				#;QaS[Kras]:QaS					#;Qel[Krell]:Qel					#;Qugh[Kruge]:Qugh				#;torgh[Torg]:torgh				#;valQIS[Valkris]:valQIS				#;