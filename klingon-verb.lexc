! REFERENCES
! https://en.wikipedia.org/wiki/Klingon_grammar
! https://www.youtube.com/watch?v=e5Did-eVQDc

Multichar_Symbols [NEG] [1P] [2P] [3P] [SG] [PL] [GUESSVERB] [OBJ-1P] [OBJ-2P] [OBJ-3P] [OBJ-SG] [OBJ-PL] [INDICATIVE] [IMPERATIVE] [REFLEXIVE] [RECIPROCAL] [REQUIRED] [WILLING] [PREPARED] [SET-UP] [AFRAID] [STATECHANGE] [RESUME] [CAUSATIVE] [UNDEFINED-SUBJ] [CAPABLE] [BEST] [CERTAIN] [UNCERTAIN] [OBVIOUS] [PERFECT] [PERFECTPURPOSE] [CONTINUOUS] [CONTINUOUSEND] [HONOR] [PERSON] [DEVERBNOUN] [INTEROGATIVE] [BEFORE] [SIMULTANEOUS] [AFTER] [CONDITIONAL] [CAUSE] [RELATIVE] [PURPOSE] [REFUSE] [EMPHASIS] [REVERSE] @U.MOOD.INDICATIVE@ @U.MOOD.IMPERATIVE@ @D.TYPE1.ON@ @P.TYPE1.ON@ @D.TYPE2.ON@ @P.TYPE2.ON@ @D.TYPE3.ON@ @P.TYPE3.ON@ @D.TYPE4.ON@ @P.TYPE4.ON@ @D.TYPE5.ON@ @P.TYPE5.ON@ @D.TYPE6.ON@ @P.TYPE6.ON@ @D.TYPE7.ON@ @P.TYPE7.ON@ @D.TYPE8.ON@ @P.TYPE8.ON@ @D.TYPE9.ON@ @P.TYPE9.ON@ @P.INDICATIVE.UNSPECIFIED@ @R.INDICATIVE.UNSPECIFIED@ @C.INDICATIVE@ @D.INDICATIVE.UNSPECIFIED@ @P.SUFFIX.CONTINUOUS@ @R.SUFFIX.CONTINUOUS@

LEXICON Root
						Prefix;


LEXICON Prefix
@U.MOOD.INDICATIVE@				IndicativePrefix;
@U.MOOD.IMPERATIVE@				ImperativePrefix;


LEXICON IndicativePrefix
[1P][SG]:0					IndicativePrefix1PSG;
[2P][SG]:0					IndicativePrefix2PSG;
[3P][SG]:0					IndicativePrefix3PSG;
[1P][PL]:0					IndicativePrefix1PPL;
[2P][PL]:0					IndicativePrefix2PPL;
[3P][PL]:0					IndicativePrefix3PPL;
@P.INDICATIVE.UNSPECIFIED@			IndicativePrefixUnspecified;

LEXICON IndicativePrefix1PSG
0:jI						Verb; ! No Object
![OBJ-1P][OBJ-SG]:0				Verb;
[OBJ-2P][OBJ-SG]:qa				Verb;
[OBJ-3P][OBJ-SG]:vI				Verb;
![OBJ-1P][OBJ-PL]:0				Verb;
[OBJ-2P][OBJ-PL]:Sa				Verb;
[OBJ-3P][OBJ-PL]:vI				Verb;

LEXICON IndicativePrefix2PSG
0:bI						Verb; ! No Object
[OBJ-1P][OBJ-SG]:cho				Verb;
![OBJ-2P][OBJ-SG]:0				Verb;
[OBJ-3P][OBJ-SG]:Da				Verb;
[OBJ-1P][OBJ-PL]:ju				Verb;
![OBJ-2P][OBJ-PL]:0				Verb;
[OBJ-3P][OBJ-PL]:Da				Verb;

LEXICON IndicativePrefix3PSG
						Verb; ! No Object
[OBJ-1P][OBJ-SG]:mu				Verb;
[OBJ-2P][OBJ-SG]:Du				Verb;
[OBJ-3P][OBJ-SG]:0				Verb;
[OBJ-1P][OBJ-PL]:nu				Verb;
[OBJ-2P][OBJ-PL]:lI				Verb;
[OBJ-3P][OBJ-PL]:0				Verb;

LEXICON IndicativePrefix1PPL
0:ma						Verb; ! No Object
![OBJ-1P][OBJ-SG]:0				Verb;
[OBJ-2P][OBJ-SG]:pI				Verb;
[OBJ-3P][OBJ-SG]:wI				Verb;
![OBJ-1P][OBJ-PL]:0				Verb;
[OBJ-2P][OBJ-PL]:re				Verb;
[OBJ-3P][OBJ-PL]:DI				Verb;

LEXICON IndicativePrefix2PPL
0:Su						Verb; ! No Object
[OBJ-1P][OBJ-SG]:tu				Verb;
![OBJ-2P][OBJ-SG]:0				Verb;
[OBJ-3P][OBJ-SG]:bo				Verb;
[OBJ-1P][OBJ-PL]:che				Verb;
![OBJ-2P][OBJ-PL]:0				Verb;
[OBJ-3P][OBJ-PL]:bo				Verb;

LEXICON IndicativePrefix3PPL
						Verb; ! No Object
[OBJ-1P][OBJ-SG]:mu				Verb;
[OBJ-2P][OBJ-SG]:nI				Verb;
[OBJ-3P][OBJ-SG]:lu				Verb;
[OBJ-1P][OBJ-PL]:nu				Verb;
[OBJ-2P][OBJ-PL]:lI				Verb;
[OBJ-3P][OBJ-PL]:0				Verb;

LEXICON IndicativePrefixUnspecified
						Verb; ! No Object
[OBJ-1P][OBJ-SG]:vI				Verb;
[OBJ-2P][OBJ-SG]:Da				Verb;
[OBJ-3P][OBJ-SG]:0				Verb;
[OBJ-1P][OBJ-PL]:wI				Verb;
[OBJ-2P][OBJ-PL]:bo				Verb;
[OBJ-3P][OBJ-PL]:lu				Verb;


LEXICON ImperativePrefix
[2P][SG]:yI					Verb;
[2P][SG][OBJ-1P][OBJ-SG]:HI			Verb;
[2P][SG][OBJ-3P][OBJ-SG]:yI			Verb;
[2P][SG][OBJ-1P][OBJ-PL]:gho			Verb;
[2P][SG][OBJ-3P][OBJ-PL]:tI			Verb;
[2P][PL]:pe					Verb;
[2P][PL][OBJ-1P][OBJ-SG]:HI			Verb;
[2P][PL][OBJ-3P][OBJ-SG]:yI			Verb;
[2P][PL][OBJ-1P][OBJ-PL]:gho			Verb;
[2P][PL][OBJ-3P][OBJ-PL]:tI			Verb;


LEXICON Verb
’ang%[to-reveal%]:’ang				RoversSuffix;
yaj%[to-understand%]:yaj			RoversSuffix;
bom%[to-sing%]:bom				RoversSuffix;
tlha’%[to-chase%]:tlha’			RoversSuffix;
bej%[to-watch%]:bej				RoversSuffix;
Qong%[to-sleep%]:Qong				RoversSuffix;
nob%[to-give%]:nob				RoversSuffix;
laD%[to-read%]:laD				RoversSuffix;
legh%[to-see%]:legh				RoversSuffix;
Sop%[to-eat%]:Sop				RoversSuffix;
tul%[to-hope%]:tul				RoversSuffix;
Qap%[to-succeed%]:Qap				RoversSuffix;
ba'%[to-sit%]:ba'				RoversSuffix;
qIp%[to-hit%]:qIp				RoversSuffix;
HoH%[to-kill%]:HoH				RoversSuffix;
tu'%[to-find%]:tu'				RoversSuffix;
Say'%[to-be-clean%]:Say'			RoversSuffix;
Quch%[to-be-happy%]:Quch			RoversSuffix;


LEXICON RoversSuffix
! "These suffixes have no defined position, and can go after the verb stem or after any suffix – even another rover – except after a type-9 suffix or where the result would be meaningless. They modify whatever directly precedes them."
[NEG]@U.MOOD.INDICATIVE@:@U.MOOD.INDICATIVE@be’			RoversSuffix;
[NEG]@U.MOOD.IMPERATIVE@:@U.MOOD.IMPERATIVE@Qo’			RoversSuffix;
[REFUSE]@U.MOOD.INDICATIVE@:@U.MOOD.INDICATIVE@Qo’			RoversSuffix;
[EMPHASIS]:qu’								RoversSuffix;
[REVERSE]:Ha’								RoversSuffix;
									AllTypeSuffixes;


LEXICON AllTypeSuffixes
			Type1Gate;
@P.TYPE1.ON@			Type2Gate;
@P.TYPE1.ON@@P.TYPE2.ON@		Type3Gate;
@P.TYPE1.ON@@P.TYPE2.ON@@P.TYPE3.ON@		Type4Gate;
@P.TYPE1.ON@@P.TYPE2.ON@@P.TYPE3.ON@@P.TYPE4.ON@		Type5Gate;
@P.TYPE1.ON@@P.TYPE2.ON@@P.TYPE3.ON@@P.TYPE4.ON@@P.TYPE5.ON@		Type6Gate;
@P.TYPE1.ON@@P.TYPE2.ON@@P.TYPE3.ON@@P.TYPE4.ON@@P.TYPE5.ON@@P.TYPE6.ON@		Type7Gate;
@P.TYPE1.ON@@P.TYPE2.ON@@P.TYPE3.ON@@P.TYPE4.ON@@P.TYPE5.ON@@P.TYPE6.ON@@P.TYPE7.ON@		Type8Gate;
@P.TYPE1.ON@@P.TYPE2.ON@@P.TYPE3.ON@@P.TYPE4.ON@@P.TYPE5.ON@@P.TYPE6.ON@@P.TYPE7.ON@@P.TYPE8.ON@		Type9Gate;
									MoodTag;


LEXICON MoodTag
[INDICATIVE]@U.MOOD.INDICATIVE@@D.INDICATIVE.UNSPECIFIED@:@U.MOOD.INDICATIVE@@D.INDICATIVE.UNSPECIFIED@			#;
[IMPERATIVE]@U.MOOD.IMPERATIVE@:@U.MOOD.IMPERATIVE@			#;


LEXICON Type1Gate
@D.TYPE1.ON@@P.TYPE1.ON@			Type1Suffix;

LEXICON Type1Suffix ! 2 Count = reflexive/reciprocal
[REFLEXIVE]:’egh				RoversSuffix;
[RECIPROCAL]:chuq				RoversSuffix;


LEXICON Type2Gate
@D.TYPE2.ON@@P.TYPE2.ON@			Type2Suffix;

LEXICON Type2Suffix ! 5 Count = volition/necessity
[REQUIRED]:nIS					RoversSuffix;
[WILLING]:qang					RoversSuffix;
[PREPARED]:rup					RoversSuffix;
[SET-UP]:beH					RoversSuffix;
[AFRAID]:vIp					RoversSuffix;


LEXICON Type3Gate
@D.TYPE3.ON@@P.TYPE3.ON@			Type3Suffix;

LEXICON Type3Suffix ! 2 Count = inceptive/inchoative
[STATECHANGE]:choH				RoversSuffix;
[RESUME]:qa’					RoversSuffix;


LEXICON Type4Gate
@D.TYPE4.ON@@P.TYPE4.ON@			Type4Suffix;

LEXICON Type4Suffix ! 1 Count = causative
[CAUSATIVE]:moH					RoversSuffix;


LEXICON Type5Gate
@D.TYPE5.ON@@P.TYPE5.ON@			Type5Suffix;

LEXICON Type5Suffix ! 2 Count = undefined subject; capability
[UNDEFINED-SUBJ]@R.INDICATIVE.UNSPECIFIED@@C.INDICATIVE@:@R.INDICATIVE.UNSPECIFIED@@C.INDICATIVE@lu				RoversSuffix;
[CAPABLE]:laH					RoversSuffix;


LEXICON Type6Gate
@D.TYPE6.ON@@P.TYPE6.ON@			Type6Suffix;

LEXICON Type6Suffix ! 4 Count = perfection; uncertainty
[BEST]:chu’					RoversSuffix;
[CERTAIN]:bej					RoversSuffix;
[UNCERTAIN]:law’				RoversSuffix;
[OBVIOUS]:ba’					RoversSuffix;


LEXICON Type7Gate
@D.TYPE7.ON@@P.TYPE7.ON@			Type7Suffix;

LEXICON Type7Suffix ! 4 Count = aspect
[PERFECT]:pu’					RoversSuffix;
[PERFECTPURPOSE]:ta’				RoversSuffix;
[CONTINUOUS]@P.SUFFIX.CONTINUOUS@:@P.SUFFIX.CONTINUOUS@taH		RoversSuffix;
[CONTINUOUSEND]:lI’				RoversSuffix;


LEXICON Type8Gate
@D.TYPE8.ON@@P.TYPE8.ON@			Type8Suffix;

LEXICON Type8Suffix ! 1 Count = honorific
[HONOR]:neS					RoversSuffix;


LEXICON Type9Gate
@D.TYPE9.ON@@P.TYPE9.ON@			Type9Suffix;

LEXICON Type9Suffix ! 11 Count = syntactic
[PERSON]:wI’					MoodTag; ! Nominalizer
[DEVERBNOUN]:ghach				MoodTag; ! Nominalizer
[INTEROGATIVE]:’a’				MoodTag; ! Modal
[OPTATIVE]:jaj					MoodTag; ! Modal
[BEFORE]:pa’					MoodTag; ! Subordinator
[SIMULTANEOUS]@R.SUFFIX.CONTINUOUS@:vIS@R.SUFFIX.CONTINUOUS@				MoodTag; ! Subordinator - It is always used in conjunction with the type-7 suffix continuous suffix -taH.
[AFTER]:DI’					MoodTag; ! Subordinator
[CONDITIONAL]:chugh				MoodTag; ! Cause and effect
[CAUSE]:mo’					MoodTag; ! Cause and effect
[RELATIVE]:bogh					MoodTag; ! Relative and purpose clause
[PURPOSE]:meH					MoodTag; ! Relative and purpose clause
