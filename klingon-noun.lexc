! REFERENCES
! https://en.wikipedia.org/wiki/Klingon_grammar
! https://www.youtube.com/watch?v=e5Did-eVQDc

Multichar_Symbols [1P] [2P] [3P] [SG] [PL] [AUGMENTATIVE] [DIMINUTIVE] [ENDEARMENT] [PL-LANGUSER] [PL-BODYPART] [PL-OTHERNOUN] [SOCALLED] [DUBITATIVE] [ACCURATE] [THIS] [THAT] [LOCATIVE] [ABLATIVE] [CAUSAL] [DATIVE] [TOPIC] @D.TYPE1.ON@ @P.TYPE1.ON@ @D.TYPE2.ON@ @P.TYPE2.ON@ @D.TYPE3.ON@ @P.TYPE3.ON@ @D.TYPE4.ON@ @P.TYPE4.ON@ @D.TYPE5.ON@ @P.TYPE5.ON@ @U.NOUNTYPE.BODYPART@ @U.NOUNTYPE.CAPABLELANGUAGE@ @U.NOUNTYPE.OTHER@

LEXICON Root
						Pronoun;
@U.NOUNTYPE.BODYPART@				NounBodyPart;
@U.NOUNTYPE.CAPABLELANGUAGE@			NounCapableOfLanguage;
@U.NOUNTYPE.OTHER@				NounOther;


LEXICON Pronoun
bIH%[they,them%]@U.NOUNTYPE.OTHER@:bIH@U.NOUNTYPE.OTHER@				TypeSuffix;
chaH%[they,them%]@U.NOUNTYPE.CAPABLELANGUAGE@:chaH@U.NOUNTYPE.CAPABLELANGUAGE@	TypeSuffix;
'e'%[that%]:'e'					TypeSuffix; ! Referring to previous part of sentence
ghaH%[he,she,him,her%]:ghaH			TypeSuffix;
jIH%[I,me%]:jIH					TypeSuffix;
maH%[we,us%]:maH				TypeSuffix;
net%[that,also%]:net				TypeSuffix; ! Similar to 'e'
'oH%[it%]:'oH					TypeSuffix;
SoH%[you-SG%]:SoH				TypeSuffix;
tlhIH%[you-PL%]:tlhIH				TypeSuffix;


LEXICON NounBodyPart
mIn%[eye%]:mIn					TypeSuffix;
ghop%[hand%]:ghop				TypeSuffix;


LEXICON NounCapableOfLanguage
puq%[child%]:puq				TypeSuffix;
vav%[father%]:vav				TypeSuffix;
jup%[friend%]:jup				TypeSuffix;
loD%[man%]:loD					TypeSuffix;
ghot%[person]:ghot				TypeSuffix;
qetwI’%[runner%]:qetwI’			TypeSuffix;
be’%[woman%]:be’				TypeSuffix;


LEXICON NounOther
choH%[change%]:choH				TypeSuffix;
veng%[city%]:veng				TypeSuffix;
qoj%[cliff%]:qoj				TypeSuffix;
ghom%[group%]:ghom				TypeSuffix;
ghop%[hand%]:ghop				TypeSuffix;
QaH%[help%]:QaH					TypeSuffix;
juH%[house%]:juH				TypeSuffix;
yuQ%[planet%]:yuQ				TypeSuffix;
pa’%[room%]:pa’					TypeSuffix;
ghu’%[situation%]:ghu’				TypeSuffix;
Hov%[star%]:Hov					TypeSuffix;
Duj%[ship%]:Duj					TypeSuffix;
targh%[targ%]:targh				TypeSuffix; ! A "targ" is a Klingon animal somewhat resembling a boar
cha%[torpedo%]:cha				TypeSuffix;


LEXICON TypeSuffix
			Type1Gate;
@P.TYPE1.ON@			Type2Gate;
@P.TYPE1.ON@@P.TYPE2.ON@		Type3Gate;
@P.TYPE1.ON@@P.TYPE2.ON@@P.TYPE3.ON@		Type4Gate;
@P.TYPE1.ON@@P.TYPE2.ON@@P.TYPE3.ON@@P.TYPE4.ON@		Type5Gate;
						#;


LEXICON Type1Gate
@D.TYPE1.ON@@P.TYPE1.ON@			Type1Suffix;

LEXICON Type1Suffix ! size, affection
[AUGMENTATIVE]:’a’				TypeSuffix;
[DIMINUTIVE]:Hom				TypeSuffix;
[ENDEARMENT]:oy					TypeSuffix;


LEXICON Type2Gate
@D.TYPE2.ON@@P.TYPE2.ON@			Type2Suffix;

! A noun does not require a plural suffix if a pronoun or pronominal prefix serves to indicate that it is plural,[10] or if it is being used in conjunction with a number.
LEXICON Type2Suffix ! plurals
[PL]@U.NOUNTYPE.CAPABLELANGUAGE@:pu’@U.NOUNTYPE.CAPABLELANGUAGE@		TypeSuffix;
[PL]@U.NOUNTYPE.BODYPART@:Du’@U.NOUNTYPE.BODYPART@				TypeSuffix;
[PL]@U.NOUNTYPE.OTHER@:@U.NOUNTYPE.OTHER@mey					TypeSuffix;
[PL]						TypeSuffix; ! A plural suffix is never obligatory

LEXICON Type3Gate
@D.TYPE3.ON@@P.TYPE3.ON@			Type3Suffix;

LEXICON Type3Suffix ! accuracy
[SOCALLED]:qoq					TypeSuffix;
[DUBITATIVE]:Hey				TypeSuffix;
[ACCURATE]:na’					TypeSuffix;


LEXICON Type4Gate
@D.TYPE4.ON@@P.TYPE4.ON@			Type4Suffix;

LEXICON Type4Suffix ! possession, determiners
[1P][SG]:wIj					TypeSuffix;
[1P][SG]@U.NOUNTYPE.CAPABLELANGUAGE@:wI’@U.NOUNTYPE.CAPABLELANGUAGE@				TypeSuffix;
[2P][SG]:lIj					TypeSuffix;
[2P][SG]@U.NOUNTYPE.CAPABLELANGUAGE@:lI’@U.NOUNTYPE.CAPABLELANGUAGE@				TypeSuffix;
[3P][SG]:Daj					TypeSuffix;
[1P][PL]:maj					TypeSuffix;
[1P][PL]@U.NOUNTYPE.CAPABLELANGUAGE@:ma’@U.NOUNTYPE.CAPABLELANGUAGE@				TypeSuffix;
[2P][PL]:raj					TypeSuffix;
[2P][PL]@U.NOUNTYPE.CAPABLELANGUAGE@:ra’@U.NOUNTYPE.CAPABLELANGUAGE@				TypeSuffix;
[3P][PL]:chaj					TypeSuffix;
[THIS]:vam					TypeSuffix;
[THAT]:vetlh					TypeSuffix;


LEXICON Type5Gate
@D.TYPE5.ON@@P.TYPE5.ON@			Type5Suffix;

LEXICON Type5Suffix ! syntactic role
[LOCATIVE]:Daq					#;
[ABLATIVE]:vo’					#;
[CAUSAL]:mo’					#;
[DATIVE]:vaD					#;
[TOPIC]:’e’					#;
